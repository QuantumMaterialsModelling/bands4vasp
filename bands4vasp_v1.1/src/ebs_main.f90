PROGRAM ebs_main
 USE MATH
 USE ebs_typs
 USE MYLATTICE
 USE ebs_methods
 IMPLICIT NONE
 TYPE( BAND ), DIMENSION(:), ALLOCATABLE :: TEBS, ROOTS
 TYPE( BAND ),  DIMENSION(:,:), ALLOCATABLE :: FP
 TYPE( LATT ) :: LATTICE
 TYPE( GRID ), DIMENSION(:), ALLOCATABLE :: KGRID
 REAL( DP ), DIMENSION(2) :: EGAP, SEGAP
 REAL( DP ), DIMENSION(:,:), ALLOCATABLE :: ROOTSXYZ
 INTEGER ::  FILETYPE, I, FFILELEN,FITLEN, SPN
 CHARACTER(50) :: PLOTFILE, OUTGRID, FERMIFILE, FITTPOINTS, FITTLINES
 CHARACTER(70) :: BANDSPEC,ACOLBAND,FSUR,FSURSPEC, SPINFILE
 CHARACTER(300) :: INCHAR,FILEDIR,FNAMEDIR
 CHARACTER(3), DIMENSION(:), ALLOCATABLE :: PATHNAME
 LOGICAL :: LFIT,LUNFOLD,LPATH,LFINISH


!>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<
!<<<<<<<< Take given parameters >>>>>>>>>
!>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<

 CALL GET_COMMAND_ARGUMENT(1,INCHAR)
 READ(INCHAR,*) N%PATH

 CALL GET_COMMAND_ARGUMENT(2,INCHAR)
 FNAMEDIR=ADJUSTL(INCHAR)

 CALL GET_COMMAND_ARGUMENT(3,INCHAR)
 READ(INCHAR,*) PAR%EFERMI

 CALL GET_COMMAND_ARGUMENT(4,INCHAR)
 READ(INCHAR,*) FILETYPE
 LUNFOLD=.TRUE.
 IF(FILETYPE==2)LUNFOLD=.FALSE.

 CALL GET_COMMAND_ARGUMENT(5,INCHAR)
 READ(INCHAR,*) I
 PAR%LSURFACE=.FALSE.
 IF(I==1)PAR%LSURFACE=.TRUE.


!>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<
!>>>>>>>>>>>>>> Read part <<<<<<<<<<<<<<<
!>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<


CALL GET_FILENAMES(FNAMEDIR,FILEDIR,PLOTFILE,BANDSPEC,OUTGRID,FERMIFILE, &
        & FITTPOINTS,ACOLBAND,FSUR,FSURSPEC,SPINFILE )

CALL READ_INPAR(PATHNAME,LPATH)

CALL READ_LATTICE(FILEDIR, LATTICE, LUNFOLD)

IF(FILETYPE==3)THEN

  CALL READHEAD_PRJCAR(FILEDIR)

  CALL READ_PRJCAR(FILEDIR,TEBS,KGRID,LATTICE)
ELSE

  CALL READHEAD_PROCAR_PRIM(FILEDIR)
  DO I=1,10
    CALL READBODY_PROCAR_PRIM(FILEDIR,TEBS,KGRID,LATTICE,LUNFOLD,LFINISH)
    IF(LFINISH)EXIT
    IF((.NOT.LFINISH).AND.(I==10))THEN
      WRITE(*,*) 'ERROR: The number of Points for the unfiltered plot &
      & is to high'
      WRITE(*,*) 'Decrease the energy interval "EDELTA1" in INPAR!'
      STOP
    END IF
  END DO
END IF

IF(PAR%SPECFUN) CALL SPECTRAL_FUNCTION(TEBS,BANDSPEC,FILETYPE)

!>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<
!>>>>>>>>>>>>>> Preparation part <<<<<<<<<<<<<<<<
!>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<

CALL SETUP_EBS()

CALL CHARACTERISE_ENERGIES( TEBS,LUNFOLD )


IF (.NOT.WRITE_PLOTDATA(PLOTFILE,ACOLBAND,TEBS,FILETYPE,SPINFILE)) STOP 1


!>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<
!>>>>>>>>>>>>>>>>> Fitting part <<<<<<<<<<<<<<<<<
!>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<

FFILELEN=LEN_TRIM(FERMIFILE)+1
FITLEN=LEN_TRIM(FITTPOINTS)+1
DO SPN=1,PAR%SPN
 IF(PAR%SPN==2)THEN
   WRITE(*,'(A,I1,A)') '*********************** Spin = ',SPN,' *************************'
   WRITE(FERMIFILE(FFILELEN:FFILELEN+5),'(A5,I1)') '.spin',SPN
   WRITE(FITTPOINTS(FITLEN:FITLEN+5),'(A5,I1)') '.spin',SPN
 END IF
 IF(PAR%ROOTSCALC)THEN
  
  CALL FINDFITTPOINTS( FP, LFIT, SPN )
  
  IF (LFIT) THEN

    FITTLINES=TRIM(FITTPOINTS)//'.lines.dat'
    IF(PAR%POLY)THEN
      CALL POLYNOM_INTERPOL_ROOTS(FP,ROOTS,TRIM(FITTLINES),KGRID,SPN)
    ELSE
      CALL LINEAR_FIT( FP, ROOTS, TRIM(FITTLINES),SPN)
    END IF
  END IF

  IF (N%FIT(SPN)>0) THEN
    CALL WRITE_FITTPOINTS(FP,TRIM(FITTPOINTS)//'.dat')

    CALL WRITE_FERMIFILE(TRIM(FERMIFILE),ROOTS,ROOTSXYZ,KGRID,LATTICE,FILETYPE)
    WRITE(*,'(I5,A)') N%FIT(SPN)," Fermiroots found and printed in "//TRIM(FERMIFILE)//'.dat'
    WRITE(*,*) ''
  ELSE
    IF(PAR%SPN==2)THEN
      WRITE(*,'(A,I1)') "No Fermiroots found for spin=",SPN
    ELSE
      WRITE(*,'(A)') "No Fermiroots found"
    END IF
    CALL ENERGYGAP( TEBS, EGAP, SEGAP, TRIM(FITTPOINTS)//'.dat', KGRID, SPN)
    WRITE(*,'(A,F12.8,A)') "Energygap of raw bandstructure: ",&
    &        EGAP(1)-EGAP(2), ' eV'
    WRITE(*,'(A,F12.8,A)') "Energygap of processed bandstructure: ",&
    &        SEGAP(1)-SEGAP(2), ' eV'
    WRITE(*,*) ''
  END IF
 END IF
 
 IF(PAR%LSURFACE) CALL FERMISURFACE(TEBS,ROOTS,ROOTSXYZ,KGRID,LATTICE,BANDSPEC,FSUR,FSURSPEC,FILETYPE,SPN)
 IF(ALLOCATED(FP))DEALLOCATE(FP)
 IF(ALLOCATED(ROOTS))DEALLOCATE(ROOTS)
  WRITE(*,'(A)') '**********************************************************'
  WRITE(*,'(A)') ''
END DO
CALL WRITE_GRID(TRIM(OUTGRID),KGRID)

END PROGRAM ebs_main
